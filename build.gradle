plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'jacoco'
}

group 'com.aquivalabs.force.ant'
version '0.12'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/valmaev/maven" }
}

def kotlin_version = '1.4.10'
def ant_version = '1.9.7'
def ant_salesforce_version = '47.0'
def xmlunit_version = '2.3.0'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-html:0.7.2'
    implementation "org.apache.ant:ant:$ant_version"
    implementation "org.valmaev.force:ant-salesforce:$ant_salesforce_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'org.testng:testng:6.9.13.6'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation "org.xmlunit:xmlunit-core:$xmlunit_version"
    testImplementation "org.xmlunit:xmlunit-matchers:$xmlunit_version"
    testImplementation "com.nhaarman:mockito-kotlin:0.12.2"
    testImplementation 'org.jsoup:jsoup:1.10.1'
}

test {
    useTestNG()
}

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': archiveVersion
        attributes 'Ant-Version': "Apache Ant $ant_version"
        attributes 'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
    }
    from('src/main/kotlin') {
        include '**/antlib.xml'
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    archiveClassifier.set 'all'
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.txt'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
        exclude 'org/apache/tools/ant/'
        exclude 'images/ant_logo_large.gif'
        exclude 'kotlinx.html.shared/'
        exclude 'kotlinx.html.shared.*.js'
    }
    with jar
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set 'sources'
}

artifacts {
    archives jar
    archives fatJar
    archives sourceJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

if (rootProject.hasProperty('bintrayUser') && rootProject.hasProperty('bintrayApiKey')) {
    bintray {
        user = getProperty('bintrayUser')
        key = getProperty('bintrayApiKey')
        publications = ['mavenJava']
        pkg {
            repo = 'maven'
            name = 'antforce'
            desc = 'A Set of extension tasks for Force.com Migration tool: reports, execution of anonymous Apex, integration with build servers, etc.'
            userOrg = user
            licenses = ['Apache-2.0']
            labels = ['ant', 'salesforce', 'force.com']
            websiteUrl = 'https://github.com/valmaev/antforce'
            vcsUrl = 'https://github.com/valmaev/antforce.git'
            issueTrackerUrl = 'https://github.com/valmaev/antforce/issues'
            githubRepo = 'valmaev/antforce'
            version {
                name = project.version
                released = new Date()
                vcsTag = "v$project.version"
            }
        }
    }
}
