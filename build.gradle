group 'com.aquivalabs.force.ant'
version '0.10'

buildscript {
    ext.kotlin_version = '1.0.6'
    ext.ant_version = '1.9.7'
    ext.xmlunit_version = '2.3.0'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlinx/kotlinx" }
    maven { url "https://dl.bintray.com/valmaev/maven" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx.html.jvm:0.5.12'
    compile "org.apache.ant:ant:$ant_version"
    compile 'org.valmaev.force:ant-salesforce:37.0'
    testCompile 'org.testng:testng:6.9.13.6'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile "org.xmlunit:xmlunit-core:$xmlunit_version"
    testCompile "org.xmlunit:xmlunit-matchers:$xmlunit_version"
    testCompile "com.nhaarman:mockito-kotlin:0.12.2"
    testCompile 'org.jsoup:jsoup:1.10.1'
}

test {
    useTestNG()
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': version
        attributes 'Ant-Version': "Apache Ant $ant_version"
        attributes 'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
    }
    from('src/main/kotlin') {
        include '**/antlib.xml'
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "org/apache/tools/ant/"
        exclude "images/ant_logo_large.gif"
        exclude "kotlinx.html.shared/"
        exclude "kotlinx.html.shared.*.js"
    }
    with jar
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

artifacts {
    archives jar
    archives fatJar
    archives sourceJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

if (rootProject.hasProperty('bintrayUser') && rootProject.hasProperty('bintrayApiKey')) {
    bintray {
        user = getProperty('bintrayUser')
        key = getProperty('bintrayApiKey')
        publications = ['mavenJava']
        pkg {
            repo = 'maven'
            name = 'antforce'
            desc = 'Set of extension tasks for Force.com Migration tool: reports, execution of anonymous Apex, integration with build servers, etc.'
            userOrg = user
            licenses = ['Apache-2.0']
            labels = ['ant', 'salesforce', 'force.com']
            websiteUrl = 'https://github.com/valmaev/antforce'
            vcsUrl = 'https://github.com/valmaev/antforce.git'
            issueTrackerUrl = 'https://github.com/valmaev/antforce/issues'
            githubRepo = 'valmaev/antforce'
            version {
                name = project.version
                released = new Date()
                vcsTag = "v$project.version"
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}